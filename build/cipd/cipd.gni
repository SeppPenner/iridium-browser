# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Build targets for constructing CIPD packages.
#
# Prepares a CIPD package and generates a package definition.
#
# TODO(crbug.com/1042819): Add support for including directories.
#
# Parameters:
#   package_definition_name: CIPD package definition filename. "cipd.yaml"
#                            if unspecified.
#   package: The path where the package will be located inside the CIPD
#            repository.
#   description: Sets the "description" field in CIPD package definition.
#   install_mode: String, should be either "symlink" or "copy". Defaults to
#                 "symlink".
#   deps: A list of targets to build prior to copying files.
#   sources: A list of files to copy into the staging root.
#
# Example:
#   cipd_package_definition("chromedriver") {
#     package = "path/to/cipd/package"
#     description = "Prebuilt test binary."
#     install_mode = "copy"
#     deps = [ "//path/to:test_binary_target" ]
#     sources = [ "//path/to:test_binary_file" ]
#   }
#
template("cipd_package_definition") {
  forward_variables_from(invoker,
                         [
                           "deps",
                           "data",
                           "data_deps",
                           "sources",
                           "testonly",
                         ])

  assert(defined(invoker.sources), "sources must be specified.")

  _install_mode = "symlink"
  if (defined(invoker.install_mode)) {
    _install_mode = invoker.install_mode
  }
  assert(_install_mode == "copy" || _install_mode == "symlink",
         "\"install_mode\" arg should be either \"copy\" or \"symlink\".")

  _package_definition_name = "cipd.yaml"
  if (defined(invoker.package_definition_name)) {
    _package_definition_name = invoker.package_definition_name
  }

  _package_root = "${target_gen_dir}/${target_name}"

  action(target_name) {
    script = "//build/cipd/prepare_cipd_package_definition.py"
    depfile = "$target_gen_dir/$target_name.d"
    _definition_path = "${_package_root}/${_package_definition_name}"
    outputs = [ _definition_path ]

    args = [
      "--pkg-name",
      invoker.package,
      "--description",
      invoker.description,
      "--pkg-root",
      rebase_path(_package_root, root_build_dir),
      "--install-mode",
      _install_mode,
      "--pkg-def",
      rebase_path(_definition_path, root_build_dir),
      "--depfile",
      rebase_path(depfile, root_build_dir),
    ]
    args += [ "--copy-files" ] + rebase_path(sources, root_build_dir)
  }
}
