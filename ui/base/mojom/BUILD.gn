# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  generate_java = true
  sources = [
    "cursor.mojom",
    "ui_base_types.mojom",
    "window_open_disposition.mojom",
  ]

  public_deps = [
    ":cursor_type",
    "//mojo/public/mojom/base",
    "//skia/public/mojom",
    "//ui/gfx/geometry/mojom",
    "//url/mojom:url_mojom_gurl",
  ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "ui.mojom.Cursor"
          cpp = "::ui::Cursor"
          copyable_pass_by_value = true
        },
      ]
      traits_sources = [ "cursor_mojom_traits.cc" ]
      traits_headers = [ "cursor_mojom_traits.h" ]
      traits_public_deps = [
        "//ui/gfx/geometry",
        "//ui/gfx/geometry/mojom:mojom_traits",
      ]
    },
    {
      types = [
        {
          mojom = "ui.mojom.DialogButton"
          cpp = "::ui::DialogButton"
        },
        {
          mojom = "ui.mojom.ModalType"
          cpp = "::ui::ModalType"
        },
      ]
      traits_headers = [ "ui_base_types_mojom_traits.h" ]
      traits_public_deps = [ "//ui/base" ]
    },
    {
      types = [
        {
          mojom = "ui.mojom.WindowOpenDisposition"
          cpp = "::WindowOpenDisposition"
        },
      ]
      traits_headers = [ "window_open_disposition_mojom_traits.h" ]
      traits_public_deps = [ "//ui/base" ]
    },
  ]
}

mojom("cursor_type") {
  generate_java = true

  sources = [ "cursor_type.mojom" ]
}

source_set("unittests") {
  testonly = true
  sources = [ "cursor_mojom_traits_unittest.cc" ]
  deps = [
    ":mojom",
    "//skia/public/mojom",
    "//testing/gtest",
    "//ui/base",
    "//ui/base/cursor",
    "//ui/events",
    "//ui/gfx/geometry/mojom:mojom_traits",
  ]
}
