# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//net/features.gni")

source_set("cert_verifier_creation") {
  sources = [
    "cert_verifier_creation.cc",
    "cert_verifier_creation.h",
  ]

  if (is_chromeos) {
    sources += [
      "cert_verify_proc_chromeos.cc",
      "cert_verify_proc_chromeos.h",
      "system_trust_store_provider_chromeos.cc",
      "system_trust_store_provider_chromeos.h",
    ]
  }

  if (trial_comparison_cert_verifier_supported) {
    sources += [
      "trial_comparison_cert_verifier_mojo.cc",
      "trial_comparison_cert_verifier_mojo.h",
    ]
  }

  deps = [
    "//base",
    "//net",
    "//services/network/public/mojom",
  ]
}

source_set("cert_verifier_creation_tests") {
  testonly = true
  sources = []

  deps = [
    ":cert_verifier_creation",
    "//base",
    "//net",
    "//net:test_support",
    "//testing/gtest",
  ]

  if (is_chromeos) {
    sources += [ "cert_verify_proc_chromeos_unittest.cc" ]
    deps += [ "//crypto:test_support" ]
  }

  if (trial_comparison_cert_verifier_supported) {
    sources += [ "trial_comparison_cert_verifier_mojo_unittest.cc" ]
  }
}
